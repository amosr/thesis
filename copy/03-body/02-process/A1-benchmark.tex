\chapter{Benchmark code}
\label{app:Benchmarks}

This appendix includes the implementations of benchmark programs that were mentioned previously in \cref{s:Benchmarks}, but were not shown there.

% \begin{lstlisting}[float=ht,caption=Folderol implementation of \Hs/priceAnalyses/,label=l:a:bench:priceAnalysesFolderol]
% priceAnalysesFolderol :: (FilePath,FilePath) -> IO (Double,Double)
% priceAnalysesFolderol (fpStock, fpMarket) = do
%   (pot,(pom,())) <- scalarIO $ \snkPOT -> scalarIO $ \snkPOM ->
%     $$(fuse $ do
%       stock  <- source [|sourceRecords fpStock|]
%       market <- source [|sourceRecords fpMarket|]
%       pot' <- priceOverTime   stock
%       pom' <- priceOverMarket stock market
%       sink pot' [|snkPOT|]
%       sink pom' [|snkPOM|])
%   return (pot,pom)
% 
% priceOverTime stock = do
%   tp <- map [|\s -> (daysSinceEpoch (time s), price s)|] stock
%   Stats.regressionCorrelation tp
% 
% priceOverMarket stock market = do
%   j  <- joinBy [|\s m -> time s `compare` time m|] stock market
%   pp <- map    [|\(s,m) -> (price s, price m)|] j
%   Stats.regressionCorrelation pp
% \end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:priceAnalysesPipes,caption=Pipes two-pass implementation of \Hs/priceAnalyses/]
priceAnalysesPipes (fpStock,fpMarket) =
  (,) <$> priceOverTime fpStock <*> priceOverMarket fpStock fpMarket

priceOverTime fpStock = 
  Fold.purely P.fold Stats.regressionCorrelation $ go
 where
  go
   = sourceRecords fpStock
   P.>-> P.map (\s -> (daysSinceEpoch $ time s, cost s)) 

priceOverMarket :: FilePath -> FilePath -> IO Double
priceOverMarket fpStock fpMarket = 
  Fold.purely P.fold Stats.regressionCorrelation $ go
 where
  go
   = joinBy (\s m -> time s `compare` time m)
        (sourceRecords fpStock)
        (sourceRecords fpMarket)
   P.>-> P.map (\(s,m) -> (cost s, cost m)) 
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:priceAnalysesStreaming,caption=Streaming implementation of \Hs/priceAnalyses/]
priceAnalysesStreaming (fpStock,fpMarket) = do
  (pom S.:> (pot S.:> (),_)) <- priceOverMarket
      (S.store priceOverTime $ sourceRecords fpStock)
      (sourceRecords fpMarket)
  return (pot,pom)

priceOverTime stock
 = Fold.purely S.fold Stats.regressionCorrelation
 $ S.map (\s -> (daysSinceEpoch (time s), cost s)) stock

priceOverMarket stock market
 = Fold.purely S.fold Stats.regressionCorrelation
 $ S.map (\(s,m) -> (cost s, cost m))
 $ joinBy (\s m -> time s `compare` time m)
   stock market
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:quickhull,caption=Quickhull skeleton parameterised by filterMax and pivots]
quickhull :: (Vector Point -> IO (Point,Point))
          -> (Line -> Vector Point -> IO (Point, Vector Point))
          -> Vector Point
          -> IO (Vector Point)
quickhull fPivots fFilterMax ps0
 | null ps0 =
   return empty
 | otherwise = do
   (l,r) <- fPivots ps0
   top   <- go l r ps0
   bot   <- go r l ps0
   return (singleton l ++ top ++ singleton r ++ bot)
 where
  go l r ps
   | null ps =
     return empty
   | otherwise = do
     (pt,above) <- fFilterMax (l,r) ps
     left       <- go l pt above
     right      <- go pt r above
     return (left ++ singleton pt ++ right)
\end{lstlisting}

% \begin{lstlisting}[float=ht,label=l:a:bench:filterMaxFolderol,caption=Folderol implementation of \Hs/filterMax/]
% filterMaxFolderol l ps = do
%   (maxim,(above,())) <- scalar          $ \snkMaxim ->
%                         vectorSize   ps $ \snkAbove ->
%    $$(fuse $ do
%       ins    <- source [|sourceOfVector ps      |]
%       annot  <- map    [|\p -> (p, distance p l)|] ins
%       above  <- filter [|\(_,d) -> d > 0        |] annot
%       above' <- map    [|fst                    |] above
%       maxim  <- maxBy  [|compare `on` snd       |] annot
%       sink maxim       [|snkMaxim               |]
%       sink above'      [|snkAbove               |])
%   return (fst maxim, above)
% \end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:filterMaxHand,caption=Hand-fused implementation of \Hs/filterMax/]
filterMaxHand l ps
 | Unbox.length ps == 0
 = return ((0,0), Unbox.empty)
 | otherwise = do
   mv <- MUnbox.unsafeNew $ Unbox.length ps
   (x,y,wix) <- go0 mv
   v <- Unbox.unsafeFreeze $ MUnbox.unsafeSlice 0 wix mv
   return ((x,y), v)
 where
  {-# INLINE go0 #-}
  go0 !mv = do
   let (x0,y0) = Unbox.unsafeIndex ps 0
   let d0 = distance (x0,y0) l
   case d0 > 0 of
    True -> do
      MUnbox.unsafeWrite mv 0 (x0,y0)
      go mv 1 1 x0 y0 d0
    False -> do
     go mv 1 0 x0 y0 d0

  {-# INLINE go #-}
  go !mv !ix !writeIx !x1 !y1 !d1
   = case ix >= Unbox.length ps of
      True -> return (x1,y1, writeIx)
      False -> do
       let (x2,y2) = Unbox.unsafeIndex ps ix
       let d2 = distance (x2,y2) l
       case d2 > 0 of
        True -> do
          MUnbox.unsafeWrite mv writeIx (x2,y2)
          case d1 > d2 of
           True -> go mv (ix + 1) (writeIx + 1) x1 y1 d1
           False -> go mv (ix + 1) (writeIx + 1) x2 y2 d2
        False ->
          case d1 > d2 of
           True -> go mv (ix + 1) writeIx x1 y1 d1
           False -> go mv (ix + 1) writeIx x2 y2 d2
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:filterMaxVectorRecompute,caption=Vector recomputed distances implementation of \Hs/filterMax/]
filterMaxVectorRecompute l ps
 = let annot1 = Unbox.map (\p -> (p, distance p l)) ps
       point  = fst
              $ Unbox.maximumBy (compare `on` snd) annot1
       annot2 = Unbox.map (\p -> (p, distance p l)) ps
       above  = Unbox.map fst
              $ Unbox.filter ((>0) . snd) annot2
   in return (point, above)
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:filterMaxPipes,caption=Pipes implementation of \Hs/filterMax/]
filterMaxPipes l ps = do
  r  <- MUnbox.unsafeNew (Unbox.length ps)
  ix <- newIORef 0
  pt <- newIORef (0,0)
  P.runEffect (sourceVector ps        P.>->
               annot                  P.>->
               filterAndMax r ix pt 0 (0,0) (-1/0))
  pt' <- readIORef pt
  ix' <- readIORef ix
  r'  <- Unbox.unsafeFreeze $ MUnbox.unsafeSlice 0 ix' r
  return (pt', r')
 where
  annot = P.map (\p -> (p, distance p l))

  filterAndMax !vecR !ixR !ptR !ix (!x,!y) !d1 = do
    lift $ writeIORef ixR ix
    lift $ writeIORef ptR (x,y)
    (p2,d2) <- P.await
    let (!p',!d') = if d1 > d2 then ((x,y),d1) else (p2,d2)
    case d2 > 0 of
     True -> do
      lift $ MUnbox.unsafeWrite vecR ix p2
      filterAndMax vecR ixR ptR (ix+1) p' d'
     False -> do
      filterAndMax vecR ixR ptR ix p' d'
\end{lstlisting}

% \begin{lstlisting}[float=ht,label=l:a:bench:compressorFolderol,caption=Folderol implementation of \Hs/compressor/]
% compressorFolderol :: Vector Double -> IO (Vector Double)
% compressorFolderol vecIns = do
%   (vecOut,()) <- vectorSize vecIns $ \snkOut -> do
%     $$(fuse $ do
%         ins     <- source    [|sourceOfVector vecIns|]
%         squares <- map       [|\x -> x * x|]       ins
%         avg     <- postscanl [|lop        |] [|0|] squares
%         mul     <- map       [|clip       |]       avg
%         out     <- zipWith   [|(*)        |] mul   ins
%         sink out             [|snkOut     |])
%   return vecOut
%  where
%   lop acc sample = acc * 0.9 + sample * 0.1
% 
%   clip mean
%    = let root    = sqrt mean
%      in  min 1.0 root / root
% \end{lstlisting}


\begin{lstlisting}[float=ht,label=l:a:bench:compressorVector,caption=Vector implementation of \Hs/compressor/]
compressorVector :: Vector Double -> IO (Vector Double)
compressorVector ins = do
  let squares = Unbox.map        (\x -> x * x) ins
  let avg     = Unbox.postscanl' lop   0       squares
  let mul     = Unbox.map        clip          avg
  let out     = Unbox.zipWith    (*)   mul     ins
  return out
\end{lstlisting}

% \begin{lstlisting}[float=ht,label=l:a:bench:compressorLopFolderol,caption=Folderol implementation of \Hs/compressor/ with low-pass]
% compressorLopFolderol :: Vector Double -> IO (Vector Double)
% compressorLopFolderol ins = do
%   (vecOut,()) <- vectorSize vecIns $ \snkOut -> do
%     $$(fuse $ do
%         ins     <- source    [|sourceOfVector vecIns|]
%         lopped  <- postscanl [|lop20k     |] [|0|] ins
%         squares <- map       [|\x -> x * x|]       lopped
%         avg     <- postscanl [|lop        |] [|0|] squares
%         mul     <- map       [|clip       |]       avg
%         out     <- zipWith   [|(*)        |] mul   lopped
%         sink out             [|snkOut     |])
%   return vecOut
% \end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:compressorLopVector,caption=Vector implementation of \Hs/compressor/ with low-pass]
compressorLopVector :: Vector Double -> IO (Vector Double)
compressorLopVector ins = do
  let lopped  = Unbox.postscanl' lop20k 0 xs
  let squares = Unbox.map (\x -> x * x) lopped
  let avg     = Unbox.postscanl' expAvg 0 squares
  let root    = Unbox.map clipRoot avg
  let out     = Unbox.zipWith (*) root lopped
  return out
\end{lstlisting}

% \begin{lstlisting}[float=ht,caption=Folderol implementation of \Hs/append2/]
% append2Folderol :: FilePath -> FilePath -> FilePath -> IO Int
% append2Folderol fpIn1 fpIn2 fpOut = do
%   (count,()) <- scalarIO $ \snkCount ->
%     $$(fuse $ do
%       in1   <- source [|sourceLinesOfFile fpIn1|]
%       in2   <- source [|sourceLinesOfFile fpIn2|]
%       aps   <- append in1 in2
%       count <- fold   [|\c _ -> c + 1|] [|0|] aps
% 
%       sink count      [|snkCount               |]
%       sink aps        [|sinkFileOfLines fpOut  |])
%   return count
% \end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:append2Conduit,caption=Conduit implementation of \Hs/append2/]
append2Conduit in1 in2 out =
  C.runConduit (sources C..| sinks)
 where
  sources = sourceFile in1 >> sourceFile in2

  sinks = do
   (i,_) <- C.fuseBoth (counting 0) (sinkFile out)
   return i

  counting i = do
   e <- C.await
   case e of
    Nothing   -> return i
    Just v -> do
     C.yield v
     counting (i + 1)
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:append2Pipes,caption=Pipes implementation of \Hs/append2/]
append2Pipes in1 in2 out = do
  h  <- IO.openFile out IO.WriteMode
  i  <- P.runEffect $ go h
  IO.hClose h
  return i
 where
  go h =
   let ins  = sourceFile in1 >> sourceFile in2
       ins' = counting ins 0
       outs = sinkHandle h
   in ins' P.>-> outs 

  counting s i = do
   e <- P.next s
   case e of
    Left _end -> return i
    Right (v,s') -> do
     P.yield v
     counting s' (i + 1)
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:append2Hand,caption=Hand-fused implementation of \Hs/append2/]
append2Hand in1 in2 out = do
  f1 <- IO.openFile in1 IO.ReadMode
  f2 <- IO.openFile in2 IO.ReadMode
  h  <- IO.openFile out IO.WriteMode
  i  <- go1 h f1 f2 0

  IO.hClose f1
  IO.hClose f2
  IO.hClose h
  return i
 where
  go1 h f1 f2 lns = do
   f1' <- IO.hIsEOF f1
   case f1' of
    True -> go2 h f2 lns
    False -> do
     l <- Char8.hGetLine f1
     Char8.hPutStrLn h l
     go1 h f1 f2 (lns + 1)

  go2 h f2 lns = do
   f2' <- IO.hIsEOF f2
   case f2' of
    True -> return lns
    False -> do
     l <- Char8.hGetLine f2
     Char8.hPutStrLn h l
     go2 h f2 (lns + 1)
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:append2Streaming,caption=Streaming implementation of \Hs/append2/]
append2Streaming in1 in2 out = do
  sinkFile out $ go (sourceFile in1) (sourceFile in2)
 where
  go s1 s2 = S.store S.length_ $ (s1 >> s2)
\end{lstlisting}

% \begin{lstlisting}[float=ht,label=l:a:bench:part2Folderol,caption=Folderol implementation of \Hs/part2/]
% part2Folderol :: FilePath -> FilePath -> FilePath -> IO (Int, Int)
% part2Folderol fpIn1 fpOut1 fpOut2 = do
%   (c1,(c2,())) <- scalarIO $ \snkC1 -> scalarIO $ \snkC2 ->
%     $$(fuse defaultFuseOptions $ do
%       in1       <- source    [|sourceLinesOfFile fpIn1|]
%       (o1s,o2s) <- partition [|\l -> length l `mod` 2 == 0|] in1
% 
%       c1        <- fold      [|\c _ -> c + 1|] [|0|] o1s
%       c2        <- fold      [|\c _ -> c + 1|] [|0|] o2s
% 
%       sink c1                [|snkC1                 |]
%       sink c2                [|snkC2                 |]
%       sink o1s               [|sinkFileOfLines fpOut1|]
%       sink o2s               [|sinkFileOfLines fpOut2|])
%   return (c1, c2)
% \end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:part2Pipes,caption=Pipes implementation of \Hs/part2/]
part2Pipes in1 out1 out2 = do
  o1 <- IO.openFile out1 IO.WriteMode
  o2 <- IO.openFile out2 IO.WriteMode
  ref <- newIORef (0,0)
  P.runEffect (sourceFile in1 P.>-> go ref o1 o2 0 0)
  IO.hClose o1
  IO.hClose o2
  readIORef ref
 where

  go ref o1 o2 !c1 !c2 = do
   lift $ writeIORef ref (c1, c2)
   v <- P.await
   case () of
    _
     | prd v -> do
      lift $ Char8.hPutStrLn o1 v
      go ref o1 o2 (c1 + 1) c2
     | otherwise -> do
      lift $ Char8.hPutStrLn o2 v
      go ref o1 o2 c1 (c2 + 1)

  prd l = ByteString.length l `mod` 2 == 0
\end{lstlisting}

\begin{lstlisting}[float=ht,label=l:a:bench:part2Hand,caption=Hand implementation of \Hs/part2/]
part2Hand in1 out1 out2 = do
  f1 <- IO.openFile in1 IO.ReadMode
  o1 <- IO.openFile out1 IO.WriteMode
  o2 <- IO.openFile out2 IO.WriteMode
  r <- go f1 o1 o2 0 0
  IO.hClose f1
  IO.hClose o1
  IO.hClose o2
  return r
 where
  go i1 o1 o2 c1 c2 = do
   i1' <- IO.hIsEOF i1
   case i1' of
    True -> return (c1, c2)
    False -> do
     l <- Char8.hGetLine i1
     case ByteString.length l `mod` 2 == 0 of
      True  -> do
        Char8.hPutStrLn o1 l
        go i1 o1 o2 (c1 + 1) c2
      False -> do
        Char8.hPutStrLn o2 l
        go i1 o1 o2 c1 (c2 + 1)
\end{lstlisting}

% \begin{lstlisting}[float=ht,label=l:a:bench:partitionAppendFail,caption=Partition / append fusion failure]
% partitionAppendFailure :: Vector Int -> IO (Vector Int)
% partitionAppendFailure xs = do
%   (ys,()) <- vectorSize xs $ \snkYs ->
%     $$(fuse $ do
%         x0    <- source [|sourceOfVector xs|]
%         as    <- filter [|even             |] x0
%         bs    <- filter [|odd              |] x0
%         -- Failure: cannot fuse append with both filters
%         asbs  <- append as bs
%         sink asbs       [|snkYs            |])
%   return ys
% \end{lstlisting}
% 
% \begin{lstlisting}[float=ht,label=l:a:bench:partitionAppend2Source,caption=Partition / append with two sources]
% partitionAppend2Source xs = do
%   (ys,()) <- vectorSize xs $ \snkYs ->
%     $$(fuse $ do
%         x0    <- source [|sourceOfVector xs|]
%         x1    <- source [|sourceOfVector xs|]
%         as    <- filter [|even             |] x0
%         bs    <- filter [|odd              |] x1
%         asbs  <- append as bs
%         sink asbs       [|snkYs            |])
%   return ys
% \end{lstlisting}
% 
% \begin{lstlisting}[float=ht,label=l:a:bench:partitionAppend2Loop,caption=Partition / append with two loops]
% partitionAppend2Loop xs = do
%   (as,(bs,())) <- vectorSize xs $ \snkAs ->
%                   vectorSize xs $ \snkBs ->
%     $$(fuse $ do
%         x0      <- source    [|sourceOfVector xs|]
%         (as,bs) <- partition [|even             |] x0
%         sink as              [|snkAs            |]
%         sink bs              [|snkBs            |])
%   (ys,()) <- vectorSize xs $ \snkYs ->
%     $$(fuse $ do
%         as'   <- source      [|sourceOfVector as|]
%         bs'   <- source      [|sourceOfVector bs|]
%         asbs  <- append as' bs'
%         sink asbs            [|snkYs            |])
%   return ys
% \end{lstlisting}


\begin{lstlisting}[float=ht,label=l:a:bench:partitionAppendVector,caption=Vector implementations of \Hs/partitionAppend/]
partitionAppendV2Loop :: Unbox.Vector Int -> IO (Unbox.Vector Int)
partitionAppendV2Loop !xs = do
  let (evens,odds) = Unbox.partition (\i -> i `mod` 2 == 0) xs
  let evens'       = Unbox.map       (\i -> i `div` 2)      evens
  let odds'        = Unbox.map       (\i -> i * 2)          odds
  let apps         = evens' Unbox.++ odds'
  return apps

partitionAppendV2Source :: Unbox.Vector Int -> IO (Unbox.Vector Int)
partitionAppendV2Source !xs = do
  let p i          = i `mod` 2 == 0
  let evens        = Unbox.filter        p          xs
  let odds         = Unbox.filter (not . p)         xs
  let evens'       = Unbox.map    (\i -> i `div` 2) evens
  let odds'        = Unbox.map    (\i -> i * 2)     odds
  let apps         = evens' Unbox.++ odds'
  return apps
\end{lstlisting}
